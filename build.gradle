buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.1.0'
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'

configurations {
    // configuration that holds jars to embed inside the jar
    embed
}

repositories {
    maven { // The repo from which to get waila
        name "Mobius Repo"
        url "http://tehnut.info/maven"
    }
    maven { // JEI
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven { // CompatLayer
        url "http://maven.k-4u.nl"
    }
    maven { // TOP
        name 'tterrag maven'
        url "http://maven.tterrag.com/"
    }
    maven { // Game stages
        url "https://maven.mcmoddev.com/"
    }
    maven { // For Serene Seasons
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }
}

group= "com.github.mcjty"
archivesBaseName = "incontrol"

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
    version = "${minecraft_version}-${forge_version}"
    runDir = "run"
    mappings = "${mcp_mappings}"
    useDepAts = true

}

dependencies {
    deobfCompile "com.github.mcjty:mcjtytools:${mcjtytools_version}"
    embed "com.github.mcjty:mcjtytools:${mcjtytools_version}"
    deobfCompile "mezz.jei:jei_1.12:4.7.0.67"
    deobfCompile "com.github.mcjty:lostcities:1.12-2.0.3"
    //deobfCompile "mcp.mobius.waila:Hwyla:1.8.10-B23_1.11:api"
    //deobfCompile "mezz.jei:jei_1.11.2:4.2.5.234"
    //deobfCompile "mcjty.theoneprobe:TheOneProbe:1.1x-1.4.6-56"
    //deobfCompile "com.github.mcjty:compatlayer:1.11.2-0.2.6"
    deobfCompile "net.darkhax.gamestages:GameStages-1.12.2:2.0.114"
    compile "serene-seasons:SereneSeasons-1.12.2:1.2.15:universal"
}

jar {
    manifest {
        attributes 'FMLAT': 'incontrol_at.cfg'
    }

    into('/META-INF/libraries') {
        from configurations.embed
    }

    doFirst {
        manifest {
            def contained = configurations.embed.collect { it.getName() };
            if (!contained.isEmpty()) {
                attributes([
                        "ContainedDeps": contained.join(' ')
                ])
            }
        }
    }
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task("uploadJars", dependsOn: "reobf") {
    description = "uploads JARs"
    if (System.getenv("local_maven") != null) {
        apply plugin: 'maven'
        uploadArchives {
            repositories {
                mavenDeployer {
                    repository(url: "file://" + System.getenv("local_maven"))
                    pom {
                        groupId = project.group
                        version = project.version
                        artifactId = project.archivesBaseName
                        project {
                            name project.archivesBaseName
                            packaging 'jar'
                            description 'In Control'
                            scm {
                                url 'https://github.com/McJty/InControl'
                                connection 'scm:git:git@github.com:McJty/InControl.git'
                                developerConnection 'scm:git:git@github.com:McJty/InControl.git'
                            }
                            issueManagement {
                                system 'github'
                                url 'https://github.com/McJty/InControl/issues'
                            }
                            licenses {
                                license {
                                    name 'MIT'
                                    distribution 'repo'
                                }
                            }
                            developers {
                                developer {
                                    id 'McJty'
                                    name 'McJty'
                                    roles { role 'developer' }
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}

String getChangelogText() {
    def changelogFile = file('changelog.txt')
    String str = ''
    int lineCount = 0
    boolean done = false
    changelogFile.eachLine {
        if (done || it == null) {
            return
        }
        if (it.size() > 1) {
            def temp = it
            if (lineCount == 0) {
                temp = "${modname} ${version}"
                temp = "<h2>$temp</h2>"
            } else if (it.startsWith('-')) {
                temp = "&nbsp;&nbsp;&nbsp;$temp"
                temp = temp.replaceAll("(\\S+\\/\\S+)#([0-9]+)\\b", "<a href=\"https://github.com/\$1/issues/\$2\">\$0</a>");
                temp = temp.replaceAll("#([0-9]+)\\b(?!<\\/a>)", "<a href=\"https://github.com/$github_project/issues/\$1\">\$0</a>");
            } else {
                temp = "<h4>$temp</h4>"
            }
            str += "$temp<br/>"
            lineCount++
            return
        } else {
            done = true
        }
    }
    return str
}

tasks.curseforge.enabled = project.hasProperty('curseforge_key')

curseforge {
    if (project.hasProperty('curseforge_key')) {
        apiKey = project.curseforge_key
    }

    project {
        id = project.projectId
        changelog = System.getenv('CHANGELOG') == null || System.getenv('CHANGELOG').equals('none') ? getChangelogText() : System.getenv('CHANGELOG')
        changelogType = 'html'
        releaseType = project.curse_type
        addGameVersion '1.12.2'
        mainArtifact(jar) {
            displayName = "${modname} - ${version}"
        }
        relations {
            optionalLibrary 'the-lost-cities'
            optionalLibrary 'game-stages'
            optionalLibrary 'serene-seasons'
        }
    }
}
